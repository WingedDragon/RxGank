apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

Properties prop = new Properties()
prop.load(new FileInputStream(file("../signing.properties")))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.sunfusheng.gank"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 5
        versionName "1.4"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        maxProcessCount 8
        javaMaxHeapSize "6g"
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        release {
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "isDebug", "false"
            signingConfig signingConfigs.release // apk签名
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }

        debug {
            buildConfigField "boolean", "isDebug", "true"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }
    }
}

//生成自定义App名称
def generateAppName(variant) {
    variant.outputs.each { output ->
        def file = output.outputFile
        output.outputFile = new File(file.parent, "Gank.IO-V" + android.defaultConfig.versionName + ".apk")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'

    // Network
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    // Rx2
    compile 'zlc.season:rxdownload2:1.1.1'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    // Others
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'com.orhanobut:logger:1.15'
    compile 'com.afollestad.material-dialogs:core:0.9.4.4'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'jp.wasabeef:recyclerview-animators:2.2.5'
}
